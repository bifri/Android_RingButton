apply plugin: 'com.android.application'
apply from: '../config/quality.gradle'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.ai.ringbutton"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 2
        versionName "1.1"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    jaxDoclet  // for Doclava Javadocs
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:21.0.3'

    jaxDoclet('com.google.doclava:doclava:1.0.6') //"some.interesting:Dependency:1.0"
}


//-------------------------
// Doclava Javadocs
//-------------------------

import com.android.builder.core.BuilderConstants
import static org.gradle.external.javadoc.JavadocMemberLevel.*

android.applicationVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(BuilderConstants.DEBUG)) {
        return; // Skip debug builds
    }
    task("generateJavadoc${variant.name.capitalize()}", type: Javadoc) {
        def projectName = "RingButton"
        description "Generate Doclava Javadocs for $variant.name."
        title = null
        source = variant.javaCompile.source
        //source = android.sourceSets.main.allJava
        destinationDir = new File("${project.getProjectDir()}/javadoc/", variant.baseName)
        // def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
        maxMemory = "1024m"
        options.docletpath = configurations.jaxDoclet.files.asType(List)
        options.memberLevel = PROTECTED
        options {
            doclet "com.google.doclava.Doclava"
            // links "http://docs.oracle.com/javase/7/docs/api/"
            // linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
            addStringOption "hdf project.name", projectName
            addStringOption "federate android", "http://d.android.com/reference"
            addStringOption "federationxml android", "http://doclava.googlecode.com/svn/static/api/android-10.xml"
            // addStringOption "overview", "http://... .html"

        }
        exclude '**/BuildConfig.java'
        //exclude '**/R.java'
    }
}
